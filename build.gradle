plugins {
    id "com.moowork.node" version "0.11"
    id 'groovy' // or 'groovy' Must be explicitly applied
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

apply plugin: 'com.moowork.node'
apply plugin: 'groovy'

node {
    version = '0.12.2'
    npmVersion = '2.7.5'
    download = true
}

repositories {
    jcenter()
}

jar {
    manifest {
        attributes 'Main-Class': 'com.nishtahir.holidayhacking.Server'
    }
}

task( hello, dependsOn: jar, type: JavaExec ) {
    main = 'hello.world.HelloWorld'
    classpath = sourceSets.main.runtimeClasspath
}

task npmCacheConfig(type: NpmTask) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = [ 'config', 'set', 'cache', npmCacheDir ]
}

task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = [ 'install' ]
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('bower_components')
    dependsOn npmPackages
}

task bowerSync(type: Sync) {
    from 'bower_components'
    into "src/main/resources/public/bower_components"
    dependsOn bowerInstall
}

task bowerPackages() {
    dependsOn bowerSync
}

clean.delete << file('src/main/resources/public/bower_components')
clean.delete << file('node_modules')
clean.delete << file('bower_components')

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile 'com.sparkjava:spark-core:2.3'
    compile 'commons-cli:commons-cli:1.3.1'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'junit:junit:4.12'
}
